plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.gms.google-services'
}

android {
    namespace 'plinko.games.mega'
    compileSdk 34

    defaultConfig {
        applicationId "plinko.games.mega"
        minSdk 24
        targetSdk 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    sourceSets {
        main.jniLibs.srcDirs = ['libs']
        main.res.srcDirs = [
                'src/main/res',
                'src/main/res/launcher',
        ]
    }
    viewBinding.enabled = true
}

configurations { natives }

dependencies {
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    implementation 'com.google.android.material:material:1.10.0'
    def gdx_version = '1.12.0'
    api "com.badlogicgames.gdx:gdx-backend-android:$gdx_version"
    natives "com.badlogicgames.gdx:gdx-platform:$gdx_version:natives-armeabi-v7a"
    natives "com.badlogicgames.gdx:gdx-platform:$gdx_version:natives-arm64-v8a"
    natives "com.badlogicgames.gdx:gdx-platform:$gdx_version:natives-x86"
    natives "com.badlogicgames.gdx:gdx-platform:$gdx_version:natives-x86_64"
    implementation "com.badlogicgames.gdx:gdx-freetype:$gdx_version"
    natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdx_version:natives-armeabi-v7a"
    natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdx_version:natives-arm64-v8a"
    natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdx_version:natives-x86"
    natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdx_version:natives-x86_64"
    implementation "com.badlogicgames.gdx:gdx-box2d:$gdx_version"
    natives "com.badlogicgames.gdx:gdx-box2d-platform:$gdx_version:natives-armeabi-v7a"
    natives "com.badlogicgames.gdx:gdx-box2d-platform:$gdx_version:natives-arm64-v8a"
    natives "com.badlogicgames.gdx:gdx-box2d-platform:$gdx_version:natives-x86"
    natives "com.badlogicgames.gdx:gdx-box2d-platform:$gdx_version:natives-x86_64"
    def core_version = '1.8.0'
    implementation "androidx.core:core-ktx:$core_version"
    def activity_version = "1.7.2"
    implementation "androidx.activity:activity-ktx:$activity_version"
    def appcompat_version = '1.6.0'
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    def navigation_version = '2.4.1'
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    def constraintLayout_version = "2.1.3"
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayout_version"
    def coroutines_version = '1.7.2'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    def dataStorePref_version = "1.0.0"
    implementation "androidx.datastore:datastore-preferences:$dataStorePref_version"
    def lottie_version = "4.2.2"
    implementation "com.airbnb.android:lottie:$lottie_version"
    def shape_drawer_version = "2.6.0"
    implementation "space.earlygrey:shapedrawer:$shape_drawer_version"

    implementation(platform("com.google.firebase:firebase-bom:32.2.2"))
    implementation("com.google.firebase:firebase-analytics-ktx")
    implementation("com.google.firebase:firebase-config-ktx")

    implementation 'androidx.browser:browser:1.6.0'
}


task copyAndroidNatives {
    doFirst {
        file("libs/armeabi-v7a/").mkdirs()
        file("libs/arm64-v8a/").mkdirs()
        file("libs/x86_64/").mkdirs()
        file("libs/x86/").mkdirs()

        configurations.natives.copy().files.each { jar ->
            def outputDir = null
            if (jar.name.endsWith("natives-arm64-v8a.jar")) outputDir = file("libs/arm64-v8a")
            if (jar.name.endsWith("natives-armeabi-v7a.jar")) outputDir = file("libs/armeabi-v7a")
            if (jar.name.endsWith("natives-x86_64.jar")) outputDir = file("libs/x86_64")
            if (jar.name.endsWith("natives-x86.jar")) outputDir = file("libs/x86")
            if (outputDir != null) {
                copy {
                    from zipTree(jar)
                    into outputDir
                    include "*.so"
                }
            }
        }
    }
}

tasks.whenTaskAdded { packageTask ->
    if (packageTask.name.contains("package")) {
        packageTask.dependsOn 'copyAndroidNatives'
    }
}

task run(type: Exec) {
    def path
    def localProperties = project.file("../local.properties")
    if (localProperties.exists()) {
        Properties properties = new Properties()
        localProperties.withInputStream { instr ->
            properties.load(instr)
        }
        def sdkDir = properties.getProperty('sdk.dir')
        if (sdkDir) {
            path = sdkDir
        } else {
            path = "$System.env.ANDROID_HOME"
        }
    } else {
        path = "$System.env.ANDROID_HOME"
    }

    def adb = path + "/platform-tools/adb"
    commandLine "$adb", 'shell', 'am', 'start', '-n', 'plinko.games.mega/plinko.games.mega.AndroidLauncher'
}